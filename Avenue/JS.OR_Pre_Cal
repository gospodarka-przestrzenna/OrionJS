'**JS.OR_Pre_Cal
' kontrala aktywnosci i oblicanie stalych regulatorow procedur
'kontaktow, konfliktow i predyspozycji
'FileDialog.Show ("*_proj_lst.dbf", "tablica projektu", "Projekt")

Lst=av.GetProject.GetDocs 
LstTb={}
Pat = "*_proj_lst.dbf".AsPattern
for each aObj in Lst
  if ((aObj.GetClass.GetClassName="Table") and (aObj.GetName=Pat)) then
    LstTb.Add(aObj)
  end
end
Tb_Prj=MsgBox.ListAsString(LstTb,"Wybierz tablice projektu","Projekt ORION")

if (Tb_Prj = NIL) then Return NIL end
_VTb_Prj=Tb_Prj.GetVtab
fldI=_VTb_Prj.FindField("Val_int")
fldT=_VTb_Prj.FindField("Val_txt")
fldD=_VTb_Prj.FindField("Val_dec")

_Pref=_VTb_Prj.ReturnValue(fldT,0)
_nr=_VTb_Prj.ReturnValue(fldI,1)
_na=_VTb_Prj.ReturnValue(fldI,2)
_nnz=_VTb_Prj.ReturnValue(fldI,3)
_nk=_VTb_Prj.ReturnValue(fldI,4)
_vc=_VTb_Prj.ReturnValue(fldD,5)


Tb_LAkt=av.FindDoc (_Pref+"_akt_lst.dbf")
VTb_LAkt=Tb_LAkt.GetVtab
fldPU=VTb_LAkt.FindField("Progr_User")
fldPC=VTb_LAkt.FindField("Progr_Cal")
VTb_LAkt.SetEditable(TRUE)

'*****kontrola rozkladu akt
Tb_Rej=av.FindDoc (_Pref+"_rej" + ".dbf")
VTb_Rej=Tb_Rej.GetVtab

for each i in 0..(_na-1)
  fldV=VTb_Rej.FindField("Akt_"+i.AsString)
  Sum=0
  for each rec in VTb_Rej
    Sum=Sum+ VTb_Rej.ReturnValue(fldV,rec)
  end
  VTb_LAkt.SetValue(fldPC,i,Sum)
  Val=VTb_LAkt.ReturnValue(fldPU,i)
  if (Sum<>Val) then 
    MsgBox.Warning("Program aktywnosci"++i.AsString++"="+Val.AsString
      +NL+"Suma rozmieszczen aktywnosci"++i.AsString++"="+Sum.AsString ,"Uwaga")
  end 
end
VTb_LAkt.SetEditable(FALSE)

'*****regulatory*********
MsgBox.Info("Obliczanie Stalych regulatorow kontaktow","Pre Calculation")

Tb_LAkt=av.FindDoc (_Pref+"_akt_lst.dbf")
VTb_LAkt=Tb_LAkt.GetVtab
fldMV=VTb_LAkt.FindField("Movable")
fldPC=VTb_LAkt.FindField("Progr_Cal")
fldDs=VTb_LAkt.FindField("Density")

_Lst_nz={}
_Lst_nzn={}
_Lst_z={}
_lst_pos_anz={}

for each i in 0..(_na-1)
  j=VTb_LAkt.ReturnValue(fldMv,i)
  if(j=1) then
    _Lst_nzn.Add(i)
    _Lst_nz.Add(i.AsString)
  else
    _Lst_z.Add(i.AsString)
  end
end

for each a in 0..(_na-1)
  _lst_pos_anz.Add(NIL)
end
for each i in 0..(_Lst_nzn.Count-1)
  _lst_pos_anz.Set(_Lst_nzn.Get(i),i)
end

LstMV={}
LstPC={}
for each a in VTb_LAkt
  LstMV.Add(VTb_LAkt.ReturnValue(fldMV,a))
  LstPC.Add(VTb_LAkt.ReturnValue(fldPC,a))
end

Tb_LKon=av.FindDoc (_Pref+"_kon_lst.dbf")
VTb_LKon=Tb_LKon.GetVtab
fldNS=VTb_LKon.FindField("Nasil")
fldAO=VTb_LKon.FindField("akt_orig")
fldAD=VTb_LKon.FindField("akt_dest")

'=======koszy kontaktow========
LstNS={}
LstAO={}
LstAD={}
for each k in 0..(_nk-1)
  LstNS.Add(VTb_LKon.ReturnValue(fldNS,k)) 
  Lst=VTb_LKon.ReturnValue(fldAO,k).AsList 
  for each i in 0..(Lst.Count-1)
    Lst.Set(i,Lst.Get(i).AsNumber)
  end
  LstAO.Add(Lst)
'  MsgBox.ListAsString(LstAO.Get(k),"","")
  Lst=VTb_LKon.ReturnValue(fldAD,k).AsList 
  for each i in 0..(Lst.Count-1)
    Lst.Set(i,Lst.Get(i).AsNumber)
  end
  LstAD.Add(Lst)
end
SUMK=0                     '#####Suma wszystkich glosow wyslanych ze wszystkich zrodel
for each k in 0..(_nk-1)
  for each aa in LstAO.Get(k)
    SUMK=SUMK+(LstPC.Get(aa)*LstNS.Get(k)) 
  end
end
'MsgBox.Info(SUMK.AsString,"Suma Kont")
LstGK={}
for each a in 0..(_na-1)
  GKZ=0                    '#####Suma nasilen wszystkich kontaktow aktywn zrodlowej a 
  for each k in 0..(_nk-1)
    if (LstAO.Get(k).FindByValue (a) > -1) then
      GKZ=GKZ+LstNS.Get(k)
    end
  end
  '''''''''''MsgBox.Info(GKZ.AsString,"GKZ")

  GKC=0                     '#####????? Suma wszystkich kontaktow wyslanych ze zrodel 
  for each k in 0..(_nk-1)
    if (LstAD.Get(k).FindByValue (a) > -1) then
      V1=0                  '##### Suma glosow wyslanych w kontakcie k gdzie a - jest aktywn celowa
      for each aa in (LstAO.Get(k))
        V1=V1+(LstPC.Get(aa)*LstNS.Get(k))
      end
      SumCel=0              '##### Suma celow w kontakcie k gdzie a - jest aktywn celowa
      for each aa in LstAD.Get(k)
        SumCel=SumCel+LstPC.Get(aa)
      end
      GKC=GKC+(V1/SumCel)   '##### Suma glosow na jednostke aktywn a jako cel we wszystkich kontaktach
    end
  end
'  MsgBox.Info(GKZ.AsString+"   "+GKC.AsString,a.AsString++"  GKZ, GKC")
  LstGK.Add((GKZ+GKC)/SUMK)
end
'##########MsgBox.ListAsString(LstGK,"GK_0","")

V1=0

for each a in 0..(_na-1)
  V1=V1+ (LstPC.Get(a)*LstGK.Get(a)) 
end
for each a in 0..(_na-1)
  LstGK.Set(a,LstGK.Get(a)/V1)
end
lst={}
for each a in _Lst_nzn
  lst.Add(LstGK.Get(a))
end
LstGK=lst
'##########MsgBox.ListAsString(LstGK,"GK","")

'=======koszy predyspozycji========

fldC=VTb_Rej.FindField("Cap")
lstGD={}
lst_PC={}
for each a in _Lst_nzn
  K_mx=0
  K_mn=0
  A_V=VTb_LAkt.ReturnValue(fldPC,a)
  lst_PC.Add(A_V)
  Ac=A_V
  Ds=VTb_LAkt.ReturnValue(fldDs,a)
  
  fldP=VTb_Rej.FindField("Pred_"+a.AsString)
  lstP={}
  lstC={}
    for each rec in VTb_Rej
    lstP.Add(VTb_Rej.ReturnValue(fldP,rec))
    lstC.Add(VTb_Rej.ReturnValue(fldC,rec))
  end
  LstDiv=lstP.Clone
  LstDiv.RemoveDuplicates
  LstDiv.Sort (True)
  for each d in LstDiv
    SuC=0
    for each r in 0..(_nr-1)
      if (lstP.Get(r)=d) then
        SuC=SuC+ lstC.Get(r) 
      end
    end
    Su=SuC*Ds
    if (Su<Ac) then
      K_mx=K_mx+(Su*(5-d))
      Ac=Ac-Su
    else
      K_mx=K_mx+(Ac*(5-d))
      Break
    end
  end
  Ac=A_V
  LstDiv.Sort (False)
  for each d in LstDiv
    SuC=0
    for each r in 0..(_nr-1)
      if (lstP.Get(r)=d) then
        SuC=SuC+ lstC.Get(r) 
      end
    end
    Su=SuC*Ds
    if (Su<Ac) then
      K_mn=K_mn+(Su*(5-d))
      Ac=Ac-Su
    else
      K_mn=K_mn+(Ac*(5-d))
      Break
    end
  end
  lstGD.Add((K_mx-K_mn)/A_V)
end
'#########MsgBox.ListAsString(lstGD,"GD_0","")

Su=0
for each i in 0..(_nnz-1)
  Su=Su + (lst_PC.Get(i)*lstGD.Get(i))
end
for each i in 0..(_nnz-1)
  lstGD.Set(i, lstGD.Get(i)/Su)
end
'#########MsgBox.ListAsString(lstGD,"GD","")


'=======koszy konfliktow========
lstGF={}
for each i in 0..(_nnz-1)
  lstGF.Add(0)
end

'=======regulatory========
lst_fld={VTb_LAkt.FindField("Mult_JK"),VTb_LAkt.FindField("Mult_JF"),VTb_LAkt.FindField("Mult_JD")}
VTb_LAkt.SetEditable(TRUE)

a=0
for each aa in _Lst_nzn
  lstJ={lstGK.Get(a),lstGF.Get(a),lstGD.Get(a)}
  lst=lstJ.Clone
  lst.Sort(False)
  mx=lst.Get(0)
  for each i in 0..2
    VTb_LAkt.SetValue(lst_fld.Get(i),aa,lstJ.Get(i)/mx)
    lstJ.Set(i,lstJ.Get(i)/mx)
  end
'###########MsgBox.ListAsString(lstJ,"J","")
  a=a+1
end

VTb_LAkt.SetEditable(FALSE)



